set(test_symlink_dir ${CMAKE_CURRENT_BINARY_DIR}/symlink_test_dir)
file(MAKE_DIRECTORY ${test_symlink_dir})
set(_s_file cmake_test_symlink.txt)
file(GENERATE OUTPUT ${test_symlink_dir}/${_s_file} CONTENT "I was generated by CMake")

set(test_symlink_dir2 ${test_symlink_dir}/link.dir)
file(CREATE_LINK ${test_symlink_dir} ${test_symlink_dir2} SYMBOLIC)

set(test_symlink_file ${test_symlink_dir2}/${_s_file}.link)
file(CREATE_LINK ${test_symlink_dir2}/${_s_file} ${test_symlink_file} SYMBOLIC)
message(VERBOSE "CMake test link file ${test_symlink_file}")

foreach(t IN ITEMS is_symlink symlink)
  add_executable(test_${t}_cpp test_${t}.cpp)
  target_include_directories(test_${t}_cpp PRIVATE ..)
  target_link_libraries(test_${t}_cpp PRIVATE ffilesystem)
  if(CMAKE_CXX_STANDARD LESS 20 AND "cxx_std_20" IN_LIST CMAKE_CXX_COMPILE_FEATURES)
    # enable source_location
    target_compile_features(test_${t}_cpp PRIVATE cxx_std_20)
  endif()
endforeach()

add_test(NAME Cpp_is_symlink COMMAND test_is_symlink_cpp ${test_symlink_file} ${test_symlink_dir2})
set_tests_properties(Cpp_is_symlink PROPERTIES FIXTURES_SETUP is_symlink)

add_test(NAME Cpp_symlink COMMAND test_symlink_cpp)
set_tests_properties(Cpp_symlink PROPERTIES
FIXTURES_SETUP symlink
FIXTURES_REQUIRED is_symlink
WORKING_DIRECTORY $<TARGET_FILE_DIR:test_symlink_cpp>
)


if(HAVE_Fortran_FILESYSTEM)

foreach(t IN ITEMS is_symlink symlink)
  add_executable(test_${t}_fortran test_${t}.f90)
  target_link_libraries(test_${t}_fortran PRIVATE ffilesystem)
  target_compile_options(test_${t}_fortran PRIVATE ${ffilesystem_fortran_test_flags})
  set_property(TARGET test_${t}_fortran PROPERTY LINKER_LANGUAGE ${ffilesystem_linker_lang})
endforeach()

add_test(NAME Fortran_is_symlink COMMAND test_is_symlink_fortran ${test_symlink_file})
set_tests_properties(Fortran_is_symlink PROPERTIES FIXTURES_SETUP is_symlink)

add_test(NAME Fortran_symlink COMMAND test_symlink_fortran)
set_tests_properties(Fortran_symlink PROPERTIES
FIXTURES_SETUP symlink
FIXTURES_REQUIRED is_symlink
SKIP_RETURN_CODE 77
)

endif()
